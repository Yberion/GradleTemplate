plugins {
    id 'java-library'
    // Apply the application plugin to add support for building a CLI application.
    id 'application'
    id 'distribution'
    id 'jacoco'

    // Spotless - keep your code spotless with Gradle
    id "com.diffplug.spotless" version "6.14.1"
    id "com.github.ben-manes.versions" version "0.45.0"
    id "io.freefair.lombok" version "6.6.1"
}

group = 'fr.brandon'
version = '19.0.0'

sourceCompatibility = JavaVersion.VERSION_19
targetCompatibility = JavaVersion.VERSION_19

compileJava.dependsOn 'spotlessApply'
test.finalizedBy 'jacocoTestReport'

repositories {
    mavenCentral()
}

dependencies {
    // tinylog2 for logging
    implementation 'org.tinylog:tinylog-api:2.6.0'
    implementation 'org.tinylog:tinylog-impl:2.6.0'
    implementation "org.mapstruct:mapstruct:1.5.3.Final"

    annotationProcessor "org.mapstruct:mapstruct-processor:1.5.3.Final"

    // Use JUnit Jupiter API for testing.
    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.2'
}

application {
    // Define the main class for the application.
    getMainClass().set('fr.brandon.template.App')
}

test {
    // Use junit platform for unit tests
    useJUnitPlatform()

    jacoco {
        // Don't check the App that only contain the main()
        excludes += [ 'fr/brandon/template/App' ]
    }
}

jacocoTestReport {
    // Display everything except App
    classDirectories.setFrom(
        fileTree(dir: 'build/classes/java/main/fr/brandon/template/')
            .filter({file -> (file.name != 'App.class') })
    )

    reports {
        xml.required = true
    }
}

spotless {
    encoding 'UTF-8'
    format 'misc', {
        target '*.gradle', '*.md'

        trimTrailingWhitespace()
        indentWithSpaces()
        endWithNewline()
    }
    java {
        target '**/*.java'
        toggleOffOn()

        licenseHeaderFile 'resources/spotless/license/LICENSE.spotless'

        importOrder()
        removeUnusedImports()
        trimTrailingWhitespace()
        indentWithSpaces()
        endWithNewline()

        eclipse().configFile('resources/eclipse/formatter/Brandon_Java.xml')
    }
}
