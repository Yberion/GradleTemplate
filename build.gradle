plugins {
    id 'java-library'

    // Apply the application plugin to add support for building a CLI application.
    id 'application'

    id 'distribution'

    // Spotless - keep your code spotless with Gradle
    id "com.diffplug.spotless" version "5.15.1"

    id "com.github.ben-manes.versions" version "0.39.0"

    id "io.freefair.lombok" version "6.2.0"

    id 'jacoco'
}

group = 'fr.brandon'
version = '1.0.0'

compileJava.options.encoding = "UTF-8"
compileTestJava.options.encoding = "UTF-8"
javadoc.options.encoding = "UTF-8"

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

compileJava.dependsOn 'spotlessApply'

test.finalizedBy 'jacocoTestReport'

repositories {
    mavenCentral()
}

dependencies {
    // tinylog2 for logging
    implementation 'org.tinylog:tinylog-api:2.4.0-M1'
    implementation 'org.tinylog:tinylog-impl:2.4.0-M1'
    implementation "org.mapstruct:mapstruct:1.5.0.Beta1"

    annotationProcessor "org.mapstruct:mapstruct-processor:1.5.0.Beta1"

    // Use JUnit Jupiter API for testing.
    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.1'
}

application {
    // Define the main class for the application.
    mainClassName = 'fr.brandon.template.App'
}

test {
    // Use junit platform for unit tests
    useJUnitPlatform()

    jacoco {
        // Don't check the App that only contain the main()
        excludes += [ 'fr/brandon/template/App' ]
    }
}

jacocoTestReport {
    // Display everything except App
    classDirectories.setFrom(
        fileTree(dir: 'build/classes/java/main/fr/brandon/template/')
            .filter({file -> !file.name.equals('App.class')})
    )

    reports {
        xml.enabled true
    }
}

spotless {
    format 'misc', {
        target '*.gradle', '*.md'

        trimTrailingWhitespace()
        indentWithSpaces()
        endWithNewline()
    }
    java {
        target '**/*.java'

        licenseHeaderFile 'resources/spotless/license/LICENSE.spotless'

        importOrder()
        removeUnusedImports()
        trimTrailingWhitespace()
        indentWithSpaces()
        endWithNewline()

        eclipse().configFile('resources/eclipse/formatter/Brandon_Java.xml')
    }
}
